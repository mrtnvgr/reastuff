desc:    MDA Overdrive
version: 0.0
tags:    gain overdrive
link:    https://github.com/mrtnvgr/jsfx

slider11:input_db=0<-48,48,0.0001>Input (dB)

slider21:drive=0<0,1,0.0001>Drive
slider22:muffle=0<0.025,1,0.0001>Muffle

slider31:output_db=0<-48,48,0.0001>Output (dB)

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init                                           
ext_noinit    =  1.0;
ext_tail_size = -2;                             

function db2gain(db) (
  pow(10, db * 0.05)
);

function sqrt2(x) (
  (x > 0) ? ( sqrt(x) ) : ( -sqrt(-x) );
);

function dist(x, drive) (
  drive * (sqrt2(x) - x) + x 
);

@slider
input_gain  = db2gain(input_db);
output_gain = db2gain(output_db);

p_spl0 = p_spl1 = 0;
filtr_coef = pow(10, -1.6 * muffle / 100);

@sample
spl0 *= input_gain;
spl1 *= input_gain;

spl0 = dist(spl0, drive);
spl1 = dist(spl1, drive);

// One pole LP filter
spl0 = p_spl0 = p_spl0 + filtr_coef * (spl0 - p_spl0);
spl1 = p_spl1 = p_spl1 + filtr_coef * (spl1 - p_spl1);

// denormals
(p_spl0 <= 1.0e-10) ? ( p_spl0 = 0; );
(p_spl1 <= 1.0e-10) ? ( p_spl1 = 0; );

spl0 *= output_gain;
spl1 *= output_gain;
